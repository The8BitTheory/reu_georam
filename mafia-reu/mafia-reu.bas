#RetroDevStudio.MetaData.BASIC:2049,BASIC V2
1 IFPEEK(53247)<>1THENPOKE808,227
10 IFZGOTO20
11 Z=1:LOAD"MF-ZEICHEN",8,1


# PRINT TO
# POKE 648,204: 204*256=52224
# VIC OFFSET
# 53272,52 - UPPER NYBBLE: 11 BIN --> 3
# 52:0011 0100
# VIC-BANK
# POKE 56576,148: BITS 0/1:00 --> BANK 3 49152-65535
# 49152 + 3072 = 52224


20 SYS49972,40960,43007,53248,52
21 POKE53265,PEEK(53265)AND239:POKE56576,148:POKE648,204:POKE53272,52
30 GOSUB100:GOSUB170:GOSUB200:GOTO1000

100 DIM LK$(12),GN$(4,10),GW(4,10),GE$(4,10),WL(4,4),SG(30),LM(12),KP(2,10)
101 DIMS9(4),TK(5),TR(5),WP(8),TS(8),TG(8),WS(8),PO(4),RA(4),NR(4),KA(4),GZ(10)
103 DIM X5%(4),X6%(4),LG%(12),MD%(7)

110 RA=53280:FR=55296:BR=52224:BL=49387:RC=49972:LC=49592:I1=49470:AD=40960
111 IE=49560:UA=167:CS=58640:SI=54272:CR=49152:PO=53240:V=53248:LH=49752
112 SO=50176:B2=BR-32768:MP=57088:CP=55296:C2=CP-32768:TT=1020:AN=1000

115 DEFFNM(LN)=50-50*(LN=3ORLN=4)-100*(LN=1)
116 DEFFNWE(X)=(X=4)OR(X=14)OR(X=27)OR(X=30)
117 DEFFNLB(X)=XAND255:DEFFNHB(X)=INT(X/256):DEFFNR(X)=INT(RND(1)*8)+8
118 MD%(0)=FN LB(B2):MD%(1)=FN HB(B2) OR 128:MD%(2)=FN LB(C2):MD%(6)=FNLB(AN)
119 MD%(3)=FNHB(C2)OR128:MD%(4)=FNLB(1020):MD%(5)=FNHB(1020):MD%(7)=FNHB(AN)

120 FORI=1TO12:READLK$(I):NEXT
121 FORI=0TO8:READWA$(I),WP(I),TS(I),TG(I),WS(I):NEXT
122 FORI=1TO10:READRA$(I):NEXT
123 FORI=0TO5:READTM$(I),TK(I),TR(I):NEXT
124 FORI=1TO10:READP(I):NEXT
125 FORI=1TO3:READTS$(I):NEXT:FORI=1TO5:READTG$(I):NEXT
126 FORI=0TO1:READAG$(I):NEXT
127 FORI=0TO2:READLM$(I):NEXT:LM=1
150 SYSBL,"KARTE":SYSRC,BR,BR+999,1024,55:SYSRC,FR,FR+999,AD,55
151 POKE211,13:POKE214,2:SYSCS:PRINT"  M A F I A  "
152 PRINTTAB(9)" (C) 1986 BY IGELSOFT "
153 POKE211,11:POKE214,22:SYSCS:PRINT" CHICAGO, 1925... "
154 GETX$:IFX$=""GOTO154
155 POKE53265,PEEK(53265)OR16:RETURN

170 POKERA,9:POKERA+1,9:INPUT" SPIELENDE (1928-1978):";X$:X9=INT(VAL(X$))
172 IFX9<1928ORX9>1978GOTO170
175 POKERA,9:POKERA+1,9:INPUT" PUNKTEWERTIGKEIT (0.1 - 2):";X$:X8=VAL(X$)
176 IFX8<0.1ORX8>2GOTO175
199 RETURN

200 REM
205 POKERA,9:POKERA+1,9:INPUT" SPIELERANZAHL:";X$:SZ=VAL(X$)
206 IFSZ<1ORSZ>4GOTO205
210 FORI=1TOSZ:PRINT" NAME SPIELER"I":";:X=15:GOSUB290:SP$(I)=X$
215 PRINT" BANDENNAME:";:X=15:GOSUB290:BN$(I)=X$
220 GOSUB300:PO(I)=18:RA(I)=1:NR(I)=1:NEXT:RETURN

290 X=PEEK(211):Y=PEEK(214):X$="":INPUTX$
291 IFX$=""ORLEN(X$)>13THENPOKE211,X:POKE214,Y:SYSCS:GOTO290
292 RETURN

300 GZ(I)=1:GN$(I,1)=SP$(I):PRINT"EIGENSCHAFTEN:"
310 PRINT"KRAFT:";:GOSUB350:KR=X
311 PRINT"INTELLIGENZ:";:GOSUB350:IN=XOR30
312 PRINT"BRUTALITAET:";:GOSUB350:BT=X
313 PRINT"ENERGIE: 5":EN=5:A=I:B=1:GOSUB1365
315 KA(I)=INT(RND(1)*5)*500+5000:IFPEEK(53247)=1THENKA(I)=500000
316 PRINT"KAPITAL:";KA(I)"$":GOTO1100

350 X=INT(RND(1)*9)*5+10 :Y=PEEK(211):PRINT"   "X;:POKE211,Y:SYSCS
355 GETX$:IFX$=""GOTO350
360 PRINT:RETURN

# RF - RANDFARBE
# HF - HINTERGRUNDFARBE
# CF - CHAR FARBE
# X$ - TEXT GEBAEUDENAME
# X$ - TEXT BEGRUESSUNG
# AW - ANZAHL ANTWORTEN
# X$ - ANTWORTEN
# LL - LOKATION LETZTE
# LA - LOKATION AKTUELL

# 1) VON DISK LESEN UND IN GEORAM SCHREIBEN - LG%(LA)
# 2) VON GEORAM IN VARIABLEN SCHREIBEN
# 3 BYTES
# 2 STRINGS
# 1 BYTE
# STRINGS ENTSPRECHEND BYTE
400 IF LG%(LA) THEN 420

410 OPEN1,8,2,LK$(LA)+",S,R"
412 INPUT#1,RF:INPUT#1,HF:INPUT#1,CF:POKERA,RF:POKERA+1,HF:POKE646,CF
414 INPUT#1,X$:PRINT" "X$": ":INPUT#1,X$:PRINT""X$:INPUT#1,AW
416 PRINT:FORI=1TOAW:INPUT#1,X$:PRINT""MID$(STR$(I),2)" "X$
418 NEXT:CLOSE1:OP=0:GOSUB 600:LG%(LA)=-1
419 POKETT,RF:POKETT+1,HF:POKETT+2,CF:POKETT+3,AW:GOSUB 640:RETURN

420 OP=1:GOSUB 640
422 POKERA,PEEK(TT):POKERA+1,PEEK(TT+1):POKE646,PEEK(TT+2):AW=PEEK(TT+3)
424 GOSUB 600

429 RETURN

# 2,3: C64 MEMORY L/H
# 4,5: REU MEMORY L/H
# 6:   REU BANK
# 7,8: COPY SIZE L/H
# 1:   OPERATION TO EXECUTE

# REU SCREEN
600 POKEMP+2,MD%(0):POKEMP+3,MD%(1)
601 POKEMP+4,FN LB((LA-1)*AN):POKEMP+5,FN HB((LA-1)*AN)
602 POKEMP+6,0
603 POKEMP+7,MD%(6):POKEMP+8,MD%(7)
604 POKEMP+1,220+OP

# REU COLOR
620 POKEMP+2,MD%(2):POKEMP+3,MD%(3)
621 POKEMP+4,FN LB((LA-1+12)*AN):POKEMP+5,FN HB((LA-1+12)*AN)
622 POKEMP+6,0
623 POKEMP+7,MD%(6):POKEMP+8,MD%(7)
624 POKEMP+1,220+OP
629 RETURN

# REU STASH VARIABLES
640 POKEMP+2,MD%(4):POKEMP+3,MD%(5)
641 POKEMP+4,FN LB((LA-1)*4):POKEMP+5,FN HB((LA-1)*4)
642 POKEMP+6,1
643 POKEMP+7,4:POKEMP+8,0
644 POKEMP+1,220+OP
649 RETURN


1000 SP=0:JA=1925
1010 SP=SP+1:IFSP=SZ+1THENSP=1:GOSUB4500:JA=JA+1/12:IFINT(JA)=X9GOTO40100
1011 GOSUB4000:IFRA(SP)=10ANDX5%(SP)>0ANDX6%(SP)>0THENSYSLH,"SIEG-PIC":GOTO40000
1012 MS=TR(TM(SP)):NR(SP)=RA(SP):LL(SP)=0:IFJO(SP)THENGOSUB25000:GOTO1010
1013 GF(SP)=INT(GF(SP)*100)/100:IFGS(SP)THENGOSUB1500:GOTO1010
1015 POKERA,0:POKERA+1,0:PRINT" "SP$(SP)" - "BN$(SP)" "
1016 PRINT"KAPITAL:"KA(SP);TAB(25)"JAHR:";:X=INT(JA):PRINTMID$(STR$(X),2);
1017 PRINT"-"MID$(STR$(1+INT((JA-X)*12)),2)
1020 PRINT"WAS WILLST DU TUN:":PRINT"1 - UEBERSICHT"
1021 PRINT"2 - DURCH DIE STADT GEHEN":PRINT"3 - BANDENKRIEG"
1022 PRINT"4 - NAECHSTER SPIELER":PRINT"GRAFIK LADEN: "LM$(LM)"  [F1]"
1030 GETX$:X=VAL(X$):IF(X<1ORX>4)ANDX$<>""GOTO1030
1031 IFX=4GOTO1010
1032 IFX$=""THENLM=LM+1+3*(LM=2):GOTO1015
1035 ONXGOSUB1200,2000,27000
1045 IFMS>0GOTO1015
1050 GOTO1010


1100 PRINT"TASTE DRUECKEN!":POKE198,0:WAIT198,1:POKE198,0:RETURN
1110 PRINT"OK (J/N)?"
1115 GETX$:IFX$=""OR(X$<>"J"ANDX$<>"N")GOTO1115
1120 RETURN

1125 PRINT"DU HAST ZU WENIG KIES!":GOTO1100
1130 IFGZ(SP)=0THENY=0:RETURN

1135 FORI=1TOGZ(SP):PRINT""MID$(STR$(I),2)" ";:A=SP:B=I:GOSUB1300
1140 POKE198,0:WAIT198,1:NEXTI:POKE198,0
1145 INPUT"NUMMER:";Y:IFY>GZ(SP)THENPRINT"";:GOTO1145
1150 IFY=0THENRETURN
1155 A=SP:B=Y:GOSUB1350:RETURN

1160 GF(SP)=GF(SP)+(X*X8):IFGF(SP)>100THENGF(SP)=100
1161 IFGF(SP)<0THENGF(SP)=0
1165 NR(SP)=INT(GF(SP)/11.1)+1:RETURN

1190 POKESI+4,0:POKESI+11,0:POKESI+18,0:POKESI+24,0:POKESI+5,0:POKESI+6,0
1191 RETURN

1200 PRINT"UEBERSICHT FUER "SP$(SP)":"
1208 IFPEEK(53247)=1THENPRINTFRE(0)
1209 PRINT"PUNKTE: "GF(SP)
1210 PRINT"RANG: "RA$(RA(SP))
1215 PRINT"TRANSPORTMITTEL: "TM$(TM(SP))" "MS"S"
1219 PRINT"ALKOHOL:"TA(SP)"FAESSER"
1220 IFAG(SP)=0GOTO1225
1221 PRINT"GEGENSTAENDE: ";:FORI=0TO1:IF(AG(SP)AND(2^I))<>0THENPRINTAG$(I)",";
1222 NEXT:PRINT" "
1225 PRINT"SCHMIERGELDER:"PL(SP)"MON.":PRINT"BEZAHLTE MIETE:"UM(SP)"MON."
1230 PRINT:GOSUB1100:PRINT"GANGSTER:":IFGZ(SP)=0THENPRINT"KEINE!":GOTO1100
1235 FORI=1TOGZ(SP):PRINT:A=SP:B=I:GOSUB1300:POKE198,0:WAIT198,1
1240 NEXT
1245 GOTO1100
1300 R=PEEK(199):L=PEEK(211):PRINTGN$(A,B):X$=GE$(A,B):POKE211,L:SYSCS
1310 POKE211,L:SYSCS:POKE199,R
1315 PRINT"E"LEFT$(X$,2)" K"MID$(X$,3,2)" I"MID$(X$,5,2)" B"RIGHT$(X$,2)
1320 POKE199,R:POKE211,L:SYSCS:PRINT"W:"WA$(GW(A,B)):RETURN

1350 X$=GE$(A,B):EN=VAL(LEFT$(X$,2)):KR=VAL(MID$(X$,3,2)):IN=VAL(MID$(X$,5,2))
1360 BT=VAL(RIGHT$(X$,2)):RETURN

1365 GE$(A,B)="":X$=MID$(STR$(EN),2):GOSUB1385
1375 X$=MID$(STR$(KR),2):GOSUB1385:X$=MID$(STR$(IN),2):GOSUB1385
1380 X$=MID$(STR$(BT),2):GOSUB1385:RETURN

1385 IFLEN(X$)=1THENX$="0"+X$
1390 GE$(A,B)=GE$(A,B)+X$:RETURN


1400 REM
1405 Y=PEEK(214):FORI=1TO3:POKE211,X:SYSCS:PRINT"                  ":NEXT
1410 POKE211,X:POKE214,Y:SYSCS:RETURN

1450 POKE214,14:SYSCS:PRINT"                                       ":RETURN
1500 GS(SP)=GS(SP)-1:POKERA,0:POKERA+1,6
1510 PRINT"DU SITZT IM KNAST."GS(SP)+1"MONAT(E) HAST"
1515 PRINT"DU NOCH VOR DIR...":GOTO1100


2000 POKERA,9:POKERA+1,15:SYSRC,1024,2023,BR,55:SYSRC,AD,AD+999,FR,54
2002 POKE646,0:IFTP(SP)=3THENPOKEBR+569,135:POKEFR+569,6
2003 IFTP(SP)=5THENPOKEBR+861,130:POKEFR+861,2
2005 IFMS<=0THENRETURN
2006 SYSI1,BR+PO(SP),FR+PO(SP),193,0,156,12
2010 GETX$:IFX$=""GOTO2010
2015 X=0:IFX$=":"THENX=-1:GOTO2030
2016 IFX$=";"THENX=1:GOTO2030
2017 IFX$="@"THENX=-40:GOTO2030
2018 IFX$="/"THENX=40:GOTO2030
2019 IFX$=""THENSYSIE:RETURN
2029 GOTO2010

2030 P=BR+PO(SP)+X:IFP<BRORP>BR+999GOTO2010
2035 SYSIE:IFPEEK(P)<>156GOTO2045
2040 PO(SP)=PO(SP)+X:MS=MS-1
2041 IFMS/20=INT(MS/20)ANDINT(RND(1)*5)=0ANDRA(SP)>3THENGOSUB6000:GOTO2060
2042 GOTO2005
2045 IFPO(SP)+X=569THENLA=13:LN=1:GOSUB23000:GOTO2060
2046 IFPO(SP)+X=861THENLA=14:LN=1:GOSUB24000:GOTO2060
2050 SYSLC,P:LA=PEEK(UA+1):LN=PEEK(UA+2):IFLA=0GOTO2005
2055 GOSUB3000:LL(SP)=20*LA+LN
2060 MS=MS-5:IFMS>0GOTO2000
2065 RETURN

3000 IFLA=9OR(LM=1ANDLM(LA)<>0)GOTO3010
3001 IFLM=0ORLM=1GOTO3007
3005 POKERA,0:POKERA+1,0:PRINT"":POKE211,9:POKE214,11:SYSCS
3006 PRINT"GRAFIK LADEN (J/N) ?":GOSUB1115:IFX$="N"GOTO3010
3007 SYSLH,LK$(LA)+"-PIC":LM(LA)=-1
3008 IFLA=13GOTO3045

3010 IF LL<>LA THEN GOTO 3015
3011 POKERA,RF:POKERA+1,HF:POKE646,CF
3012 SYSRC,AD+1000,AD+1999,BR,54:SYSRC,AD+2000,AD+2999,FR,54:GOTO3040

3015 GOSUB 400

3036 SYSRC,BR,BR+999,AD+1000,55:SYSRC,FR,FR+999,AD+2000,55:LL=LA

3040 GETX$:W=VAL(X$):IFW<1ORW>AWGOTO3040
3045 IFW=AWTHENMS=MS-5:RETURN
3050 PRINT""
3100 IFLA>10GOTO3110
3105 ONLAGOTO10000,12000,13000,14000,15000,16000,17000,18000,19000,20000
3110 ONLA-10GOTO21000,22000,23000

4000 REM
4005 POKERA,8:POKERA+1,8:PRINT"":POKE211,15-LEN(SP$(SP))/2:POKE214,10:SYSCS
4006 PRINT"SPIELER "SP$(SP):PRINTTAB(10)"IST AN DER REIHE..."
4010 SYSSO,9:FORT=1TO1000:NEXT:FORI=1TOGZ(SP)
4015 A=SP:B=I:GOSUB1350:EN=EN+INT(KR/10)+1
4020 X=2+INT(KR/4)+INT(BT/4):IFEN>XTHENEN=X
4025 GOSUB1365:NEXTI
4030 IFRA(SP)<>NR(SP)THENRA(SP)=NR(SP):GOSUB4200
4040 IFKR(SP)ANDGS(SP)=0THENGOSUB4300
4041 IFKG(SP)<>0ANDKK(SP)<>0THENGOSUB4400
4045 IFUM(SP)=0GOTO4050
4046 UM(SP)=UM(SP)-1:IFUM(SP)=0THENUM(SP)=1:GOSUB4600
4050 PL(SP)=PL(SP)+(PL(SP)>0)
4055 IFINT(RND(1)*8)=0THENAG(SP)=AG(SP)AND254
4056 IFINT(RND(1)*8)=0THENAG(SP)=AG(SP)AND253
4060 IFTP(SP)=4THENGOSUB31000
4090 RETURN

4200 SYSBL,"WANT":POKE211,20-INT(LEN(BN$(SP))/2):POKE214,8:SYSCS:PRINT""BN$(SP)
4210 POKE211,19:POKE214,15:SYSCS:PRINT""SP$(SP)"."
4215 POKE211,25:POKE214,20:SYSCS:PRINT""MID$(STR$(GF(SP)),2)" P."
4220 POKE211,14:SYSCS:PRINT""RA$(RA(SP)):POKE198,0:WAIT198,1:POKE198,0:RETURN

4300 POKERA,11:POKERA+1,11:PRINT" "SP$(SP)"! "
4305 KZ(SP)=KZ(SP)+(KZ(SP)>0):IFKZ(SP)=0GOTO4350
4306 PRINT"DU HAST"KR(SP)"$ SCHULDEN. DIR BLEIBEN"
4308 PRINT"NOCH"KZ(SP)+1"MONAT(E), SIE"
4309 PRINT"ABZUZAHLEN!":GOTO1100
4350 PRINT"DER KREDITHAI HETZT SEINE EINTREIBER":PRINT"AUF DICH...":GOSUB1100
4355 BN$(0)="EINTREIBER":W=3:E=30:GZ(0)=5:KF$="KS":GOSUB5000:IFS=1THENRETURN
4365 PRINT"DIE KERLE NEHMEN DEIN GANZES GELD MIT":PRINT"UND VERSCHWINDEN..."
4370 KA(SP)=0:KR(SP)=0:KZ(SP)=0:GOTO1100
4400 POKERA,11:POKERA+1,11
4405 IFINT(RND(1)*3)GOTO4410
4406 PRINT"IN DEINEM KREDITINSTITUT HERRSCHT":PRINT"FLAUTE.":GOTO1100
4410 P=INT(RND(1)*KK(SP)/20+KK(SP)/10):KA(SP)=KA(SP)+P
4415 PRINT"DEIN KREDITGESCHAEFT FLORIERT UND DU"
4420 PRINT"MACHST"P"$ EINNAHMEN.":GOTO1100

4500 POKERA,2:POKERA+1,2:PRINT" SPIELSTAND ";:X=INT(JA)
4501 PRINTMID$(STR$(X),2)"-"MID$(STR$(1+INT((JA-X)*12)),2)" "
4505 PRINT"SPIELER:        KAPITAL: PUNKTE:"
4510 FORI=1TOSZ:PRINT""SP$(I);TAB(15);KA(I)"$";TAB(26);GF(I):NEXT
4515 PRINT:GOTO1100

4600 POKERA,2:POKERA+1,2
4605 P=INT(RND(1)*100)+200:IFP>KA(SP)THENP=KA(SP):IFP=0GOTO4650
4610 PRINT"DU HAST DEINE MIETE NICHT PUENKTLICH"
4615 PRINT"GEZAHLT. MAN HAT DIR MOEBEL IM WERT VON"
4620 PRINT""P"$ GEPFAENDET!":KA(SP)=KA(SP)-P:GOTO1100
4650 PRINT"DEINE WOHNUNG WIRD DIR GEKUENDIGT!"
4651 PRINT"DEINE GANGSTER SUCHEN SICH EINEN ANDEREN":PRINT"BOSS...":GZ(SP)=1
4652 GOTO1100

5000 FORI=1TOGZ(0):GW(0,I)=W:EC(I)=E:NEXT
5010 KS(1)=SP:KS(2)=0:GOTO30000

6000 POKERA,14:POKERA+1,14:PRINT" STRASSENSPERRE!!! "
6010 PRINT"AUSWEISKONTROLLE! DER POLIZIST VERLANGT":PRINT"DEINE PAPIERE!"
6015 FORT=1TO2000:NEXT:IFINT(RND(1)*3)=0GOTO6025
6016 IF(AG(SP)AND2)<>0GOTO6030
6017 IFTA(SP)GOTO6035
6018 IF(AG(SP)AND1)<>0GOTO6025
6020 PRINT"ER HAT EINEN STECKBRIEF VON DIR!":GOSUB1100:GOTO26020
6025 PRINT"ER HAT NICHTS ZU BEANSTANDEN.":GOTO1100
6030 PRINT"DEIN BLUETEN-SCHWINDEL IST AUFGEFLOGEN!":GOSUB1100:GOTO26020
6035 PRINT"DER KERL HAT DEINE ALKOHOLFAESSER ENT-"
6036 PRINT"DECKT! ES IST AUS...":TA(SP)=0:GOSUB1100:GOTO26020

#################
# SCHLUPFWINKEL #
#################
10000 REM
10005 ONWGOTO10010,10100
10010 IFUK(LN)<>0THENPRINT"'NICHTS MEHR FREI!'":GOTO1100
10020 P=FNM(LN):PRINT"'GUT. PRO MONAT KOSTET DAS"P"$":PRINT"MIETE.'":GOTO10025
10025 INPUT"WIEVIELE MONATE WILLST DU MIETEN";X
10030 IFX=0ORX<0 THENNM=1:RETURN
10035 IFKA(SP)<X*PGOTO1125
10040 KA(SP)=KA(SP)-X*P:UK(LN)=SP:UM(SP)=UM(SP)+X
10045 PRINT"'GUTEN TAG, DER HERR!'":GOTO1100
10100 IFUK(LN)<>SPTHENPRINT"DU WOHNST HIER NICHT!":NM=1:GOTO1100
10105 GOTO10020


#######
# PUB #
#######
12000 REM
12005 ONWGOTO12010,12100,12200,12300

12010 IFLN=4ORLN=5GOTO12020
12015 IFINT(RND(1)*2)=0GOTO12050
12016 PRINT"'WAS? ALKOHOL? IST DOCH VERBOTEN!'":GOTO1100
12020 X=INT(RND(1)*200)+100:P=INT(RND(1)*5)+5
12021 PRINT"'ICH HABE"
12022 PRINTX"FAESSER FUER"P"$ PRO FASS.'"
12025 Q=TK(TM(SP))-TA(SP):IFQ<XTHENX=Q
12027 PRINT"WIEVIELE WILLST DU (0-"MID$(STR$(X),2)")";:INPUTY
12028 IFY>XTHENPRINT"";:GOTO12027
12029 IFY=0THENRETURN
12030 IFKA(SP)<Y*PGOTO1125
12035 TA(SP)=TA(SP)+Y:KA(SP)=KA(SP)-P*Y:X=2:GOSUB1160:RETURN
12050 X=INT(RND(1)*20)+10
12055 PRINT"'NEIN. VIELLEICHT HAST DU ABER WAS FUER"
12056 PRINT"MICH? ICH ZAHLE"X"$ PRO FASS!'"
12060 PRINT"WIEVIEL WILLST DU ABGEBEN (0-"MID$(STR$(TA(SP)),2)")";:INPUTY
12061 IFY>TA(SP)THENPRINT"";:GOTO12060
12065 IFY=0THENRETURN
12070 PRINT"'BESORG NOCH MEHR(GIER)!'":GOTO12075
12075 KA(SP)=KA(SP)+Y*X:TA(SP)=TA(SP)-Y:GOTO1100

12100 IFRA(SP)>4GOTO12103
12101 PRINT"ALS "RA$(RA(SP))" KANNST DU NOCH"
12102 PRINT"KEINE EIGENEN LEUTE HABEN!":GOTO1100
12103 FORI=1TO5:IFUK(I)=SPGOTO12105
12104 NEXTI:PRINT"DU BRAUCHST EINE UNTERKUNFT FUER DIE    GANGSTER!":GOTO1100
12105 IFGZ(SP)=10THENPRINT"MAXIMAL 10 GANGSTER!":GOTO1100
12106 Y=0:FORI=1TO30:Y=Y-(SG(I)=0):NEXT:IFY>3THENY=3
12107 X=INT(RND(1)*(Y+1)):IFX=0ORLN=3THENPRINT"ES IST NIEMAND DA.":GOTO1100
12108 FORI=1TOX
12110 G(I)=INT(RND(1)*30)+1:IFSG(G(I))GOTO12110
12111 IFI=1GOTO12115
12112 FORJ=1TOI-1:IFG(J)=G(I)GOTO12110
12113 NEXTJ
12115 IFFNWE(G(I))GOTO12120
12116 PRINT"EIN GANGSTER KOMMT, UM MIT DIR ZU    SPRECHEN...":GOTO12125
12120 PRINT"EINE DAME SCHEINT SICH FUER DEINE"
12121 PRINT"GANG ZU INTERESSIEREN..."
12125 OPEN1,8,2,"GAN"+MID$(STR$(G(I)),2)+",S,R":INPUT#1,GN$:INPUT#1,GW
12126 INPUT#1,GE$:INPUT#1,P:P=P+1000:INPUT#1,X$:CLOSE1
12130 Y$="ER":IFFNWE(G(I))THENY$="SIE"
12131 PRINT""Y$" WIRD "GN$" GENANNT.":PRINT""X$"."
12135 PRINT""Y$" VERLANGT"P"$ VORSCHUSS, WENN"
12136 PRINTY$" SICH DIR ANSCHLIESSEN SOLL.":PRINT:GOSUB1110:IFX$="N"GOTO12175
12140 IFKA(SP)<PTHENGOSUB1125:GOTO12175
12145 IFGZ(SP)=10GOTO12005
12160 KA(SP)=KA(SP)-P:GZ(SP)=GZ(SP)+1:GN$(SP,GZ(SP))=GN$
12165 GW(SP,GZ(SP))=GW:GE$(SP,GZ(SP))="05"+GE$:SG(G(I))=1
12170 PRINT"NUN HAST DU"GZ(SP)"GANGSTER!":GOSUB1100
12175 NEXTI:RETURN

12200 IFRA(SP)>3GOTO12210
12205 PRINT"'DU BIST NOCH ZU UNERFAHREN!'":GOTO1100

12210 IFINT(RND(1)*3)THENPRINT"'LEIDER HABE ICH NICHTS FUER DICH!'":GOTO1100

12215 P=1000+INT(RND(1)*3)*500:PRINT"'ICH HABE ETWAS GEHOERT...ABER DAS"
12216 PRINT"KOSTET DICH"P"$. ";:GOSUB1110:IFX$="N"THENRETURN
12220 IFKA(SP)<PGOTO1125
12225 KA(SP)=KA(SP)-P:TP(SP)=INT(RND(1)*5)+1
12226 ONTP(SP)GOTO12230,12235,12240,12245,12250
12230 PRINT"'IM POSTZUG SOLL DEMNAECHST EINE GROSSE"
12231 PRINT"LADUNG DIAMANTEN TRANSPORTIERT WERDEN!'":GOTO1100

12235 PRINT"'DIE BANK AN DER HAUPTSTRASSE ERHAELT"
12236 PRINT"IN NAECHSTER ZEIT 500 KG GOLDBARREN!'":GOTO1100

12240 PRINT"'EIN GELDTRANSPORT WIRD DURCH UNSERE"
12241 PRINT"STADT FAHREN. ES IST EIN BLAUER WAGEN"
12242 PRINT"WAGEN, DU WIRST IHN SEHEN!'":GOTO1100

12245 PRINT"'MIT 5000 $ KANNST DU DICH AN EINEM"
12246 PRINT"GROSSEN WAFFENSCHMUGGEL BETEILIGEN!'":PRINT
12247 GOSUB1110:IFX$="N"THENTP(SP)=0:RETURN
12248 IFKA(SP)<5000THENTP(SP)=0:GOTO1125
12249 KA(SP)=KA(SP)-5000:PRINT"'DU WIRST ES NICHT BEREUEN...'":GOTO1100
12250 PRINT"'PSST...ICH HABE DA EINEN SEHR WICHTIGEN"
12251 PRINT"AUFTRAG! DER BUERGERMEISTER SOLL UMGE-"
12252 PRINT"LEGT WERDEN, ER HAT ABER LEIBWAECHTER!'":GOTO1100

12300 IFRA(SP)<4GOTO12302
12301 PRINT"ALS '"RA$(RA(SP))"' FINDEST DU WAS":PRINT"BESSERES!":GOTO1100

12302 IFINT(RND(1)*5)=0THENPRINT"NIEMAND HAT EINEN JOB FUER DICH.":GOTO1100

12305 X=INT(RND(1)*4)+1:ONXGOTO12306,12310,12315,12320
12306 PRINT"DER INHABER DER BAHNHOFSKNEIPE WILL"
12307 PRINT"DICH FUER 3 MONATE ALS RAUSSCHMEISSER"
12308 PRINT"ANSTELLEN.":JD(SP)=3:P=INT(RND(1)*1000)+2000:GOTO12330
12310 PRINT"DU KANNST 2 MONATE ALS CROUPIER IN GEOR-"
12311 PRINT"GES SPIELHOELLE ARBEITEN.":P=INT(RND(1)*500)+1000:JD(SP)=2:GOTO12330

12315 PRINT"DAS 'LE ROI'-HOTEL SUCHT EINEN TUER-"
12316 PRINT"STEHER FUER 2 MONATE.":P=INT(RND(1)*500)+2000:JD(SP)=2:GOTO12330

12320 PRINT"DU ERHAELST DAS FOTO EINES MANNES, DER"
12321 PRINT"IM 'RED HOUSE' ABGESTIEGEN IST. DU":PRINT"SOLLST IHN UMLEGEN!"
12322 P=INT(RND(1)*500)+2000:JD(SP)=1
12330 PRINT"DU BEKOMMST DAFUER"P"$. ";:GOSUB1110:IFX$="N"THENRETURN
12335 PRINT"DU HAST DEN JOB!":JO(SP)=X:JL(SP)=P:MS=0:GOTO1100

13000 REM
13005 ONWGOTO13010,13100
13010 PRINT"OK, WIR HABEN FOLGENDES:":PRINT
13011 IFLN=1THENA=3:B=7:IFINT(RND(1)*3)=0ANDRA(SP)>5THENGOSUB13090
13012 IFLN=2THENA=1:B=5
13013 IFLN=3THENA=1:B=4
13015 FORI=ATOB:PRINTMID$(STR$(I),2)" - "WA$(I);WP(I)"$":NEXT
13020 INPUT"IHRE WAHL:";X:IFX=0THENRETURN
13021 IFX<AORX>BTHENPRINT"";:GOTO13020
13025 IFKA(SP)<WP(X)THENGOSUB1125:GOTO13010
13035 GOSUB13500:PRINT"FUER WELCHEN GANGSTER:":GOSUB1130:IFY=0GOTO13010
13050 IFIN<40ANDX>5THENPRINT"ZU WENIG INTELLIGENZ!":GOSUB1100:GOTO13035
13055 IFKR<20AND(X=2ORX=3)THENPRINT"ZU WENIG KRAFT!":GOSUB1100:GOTO13035
13060 IFBT<40AND(X=3ORX>6)THENPRINT"NICHT BRUTAL GENUG!":GOSUB1100:GOTO13035
13065 IFGW(SP,Y)=0THENQ=0:GF(SP)=GF(SP)-X8*1*(GF(SP)<100):GOTO13075
13070 Q=INT(WP(GW(SP,Y))/1.5):PRINT"DER HAENDLER BIETET DIR"Q"$ FUER"
13071 PRINT"DEINE ALTE WAFFE. ";:GOSUB1110:IFX$="N"GOTO13010
13072 IFX>GW(SP,Y)THENGF(SP)=GF(SP)-X8*1*(GF(SP)<100):GOTO13075
13073 GF(SP)=GF(SP)+X8*2*(GF(SP)>0)
13075 KA(SP)=KA(SP)+Q-WP(X):GW(SP,Y)=X
13080 PRINT"DU HAST NUN DIE NEUE WAFFE!":GOTO1100
13090 PRINT"...BRANDHEISS! EINE LADUNG ARMY-HAND-"
13091 PRINT"GRANATEN IST GERADE EINGETROFFEN!":B=8:PRINT:RETURN
13100 IFGN$(SP,1)=""THENPRINT"LEIDER HAST DU NICHT EINEN GANGSTER!":GOTO1100
13101 PRINT"WEN SOLL ICH TRAINIEREN:"
13102 GOSUB1130:IFY=0THENRETURN
13103 IFRA(SP)<5GOTO13110
13105 PRINT"(S)CHIESSTAND ODER (T)RAININGSCAMP ?"
13106 GETX$:IFX$<>"S"ANDX$<>"T"GOTO13106
13107 IFX$="T"GOTO13150
13110 P=800+200*RA(SP):PRINT"DAS KOSTET DICH"P"$. ";
13115 GOSUB1110:IFX$="N"THENRETURN
13116 IFKA(SP)<PGOTO1125
13120 PRINT"'DEIN GANGSTER UEBT AN MEINEM SCHIESS-"
13121 PRINT"STAND...'"
13125 KA(SP)=KA(SP)-P:KR=KR+5:IFKR>99THENKR=99
13126 IN=IN+3-2*(LN=1):IFIN>99THENIN=99
13127 BT=BT+2-3*(LN=2):IFBT>99THENBT=99
13130 GOSUB1365:X=1:GOSUB1160:GOTO1100
13150 P=2500+500*RA(SP):PRINT"DAS KOSTET DICH"P"$. ";
13155 GOSUB1110:IFX$="N"THENRETURN
13160 IFKA(SP)<PGOTO1125
13165 PRINT""GN$(A,B)" IST IM TRAININGSCAMP...":FORT=1TO2000:NEXT
13170 KA(SP)=KA(SP)-P:IN=IN+FNR(0):IFIN>99THENIN=99
13171 BT=BT+FNR(0):IFBT>99THENBT=99
13172 KR=KR+FNR(0):IFKR>99THENKR=99
13175 GOSUB1365:X=2:GOSUB1160:PRINT"...DA IST ER WIEDER!":GOTO1100
13500 PRINT" WAFFE: "WA$(X)" ":PRINT:FORJ=1TO6:PRINT"       ":NEXTJ
13505 POKEV,32:POKEV+1,78:POKEV+39,0:POKEV+23,1:POKEV+29,1
13510 POKEPO,23+X:POKEV+21,1:PRINT""TAB(8)"PREIS:"WP(X)"$"
13515 PRINTTAB(8)"TREFFGENAUIGKEIT: "TS$(INT(TS(X)/2))
13520 PRINTTAB(8)"WIRKUNG: "TG$(INT(TG(X)/4)+1)
13525 PRINT" TASTE DRUECKEN!":POKE198,0:WAIT198,1:POKE198,0:POKEV+21,0:RETURN

14000 REM
14005 ONWGOTO14006,14100
14006 PRINT"DER VERKAEUFER ZEIGT DIR EINE AUSWAHL"
14007 PRINT"DER MODELLE. ZUM KAUF DIE ENTSPRECHENDE"
14008 PRINT"NUMMERNTASTE (0=ENDE) DRUECKEN!":GOSUB1100:PRINT"";
14010 X=2:IFLN=2THENX=X+1
14011 FORI=0TOX:Y=X-1
14012 FORI=0TOX:PRINT:FORJ=1TO5:PRINT"         ":NEXTJ
14013 POKEPO+I,20+I:POKEV+2*I,38:POKEV+1+2*I,60+48*I:POKEV+39+I,0
14014 PRINTTAB(10)""CHR$(49+I)" "TM$(I+1):PRINTTAB(11);3000+1000*I"$"
14015 NEXT:Y=2^(X+1)-1:POKEV+23,Y:POKEV+29,Y:POKEV+21,Y
14020 GETX$:IFX$=""GOTO14020
14025 Y=VAL(X$):IFY=0THENPOKEV+21,0:RETURN
14030 IF(Y-1)>XGOTO14020
14035 POKEV+21,0:PRINT"":P=3000+1000*(Y-1):IFKA(SP)<PTHENGOSUB1125:GOTO14006
14040 IFTM(SP)=0THENQ=0:GOTO14050
14045 Q=1000+1000*TM(SP):IFTM(SP)=5THENQ=1000
14046 PRINT"MAN BIETET DIR"Q"$ FUER DEINE ALTE"
14047 PRINT"SCHAUKEL. ";:GOSUB1110:IFX$="N"GOTO14006
14050 KA(SP)=KA(SP)-P+Q:MS=TR(Y)-TR(TM(SP))+MS:TM(SP)=Y
14051 PRINT"DER VERKAEUFER REICHT DIR SCHLUESSEL"
14052 PRINT"UND PAPIERE.":GOTO1100
14100 IFLN<>4ANDINT(RND(1)*3)<>0THENPRINT"ES SIND ZUVIELE LEUTE HIER!":GOTO1100
14101 PRINT"WER SOLL DEN WAGEN AUFBRECHEN:":GOSUB1130:IFY=0THENRETURN
14110 IFINT(RND(1)*(IN/40+KR/30))=0GOTO14120
14115 PRINT"YEAH!DIE KARRE IST OFFEN! DU MACHST"
14116 PRINT"DICH DAMIT AUS DEM STAUB.";:IFTM(SP)=0THENPRINT:GOTO14118
14117 PRINT" UND MUSST LEI- DER DEINEN ALTEN WAGEN STEHEN LASSEN."
14118 TM(SP)=5:GOTO1100
14120 PRINT"LEIDER ERWISCHT DICH DER BESITZER DER   KARRE!":GOSUB1100
14125 BN$(0)="WAGENBESITZER":GZ(0)=1:E=30:W=5:KF$="KS":GOSUB5000:IFS=2GOTO26020
14130 PRINT"NACH DEM MORD AN DEM BESITZER MUSST DU"
14131 PRINT"VERSCHWINDEN UND DEN WAGEN STEHEN":PRINT"LASSEN!":GOTO1100

#############
# KREDITHAI #
#############
15000 REM
15005 ONWGOTO15010,15050,15100,15200,15300
15010 IFKR(SP)THENPRINT"ZAHLE ERSTMAL DEINE ALTEN SCHULDEN AB!":GOTO1100
15015 PRINT"WIEVIEL GELD WILLST DU LEIHEN":INPUT"(0-5000 $)";X$:X=VAL(X$)
15020 IFX=0THENRETURN
15021 IFX<0ORX>5000THENPRINT"":GOTO15015
15025 PRINT"DU HAST 6 MONATE ZEIT, DIE SCHULDEN"
15026 PRINT"ZURUECK ZU ZAHLEN!"
15030 KR(SP)=KR(SP)+X:KA(SP)=KA(SP)+X:KZ(SP)=6:GOTO1100
15050 PRINT"WIEVIEL WILLST DU ZURUECKZAHLEN"
15051 PRINT"(0-"MID$(STR$(KR(SP)),2)")";:INPUTX$:X=VAL(X$):IFX=0THENRETURN
15055 IFX<0ORX>KR(SP)THENPRINT"":GOTO15050
15060 IFKA(SP)<XGOTO1125
15065 KR(SP)=KR(SP)-X:KA(SP)=KA(SP)-X:IFKR(SP)=0GOTO15075
15070 PRINT"OK, DU HAST JETZT NOCH"KR(SP)"$":PRINT"SCHULDEN!":GOTO1100
15075 KZ(SP)=0:PRINT"DU HAST DIE SCHULDEN ZURUECKGEZAHLT!":GOTO1100
15100 IFKG(SP)=LNGOTO15150
15105 IFKG(SP)THENPRINT"DU HAST SCHON EIN KREDITGESCHAEFT!":GOTO1100
15106 IFKR(SP)THENPRINT"ZAHLE ERSTMAL DEINE EIGENEN SCHULDEN!":GOTO1100
15107 FORI=1TOSZ:IFKG(I)<>LNTHENNEXT:GOTO15110
15108 PRINT"DER LADEN GEHOERT "SP$(I)"!":GOTO1100
15110 P=INT(RND(1)*11)*100+5000:PRINT"DER JETZIGE BESITZER VERLANGT"P"$."
15111 PRINT:GOSUB1110:IFX$="N"THENRETURN
15115 IFKA(SP)<PGOTO1125
15120 KA(SP)=KA(SP)-P:KG(SP)=LN:PRINT"DER LADEN GEHOERT NUN DIR!":GOSUB1100
15125 GOTO15200
15150 P=INT(RND(1)*11)*100+4500:PRINT"EIN KAEUFER BIETET DIR"P"$ FUER DEN"
15151 PRINT"LADEN. WILLST DU VERKAUFEN (J/N) ?":GOSUB1115:IFX$="N"THENRETURN
15155 KA(SP)=KA(SP)+P:KG(SP)=0:RETURN
15200 IFKG(SP)<>LNTHENPRINT"DIESER LADEN GEHOERT DIR NICHT!":GOTO1100
15205 PRINT"DU HAST"KK(SP)"$ IM KREDITGESCHAEFT":PRINT"(MAXIMAL 5000 $)."
15206 PRINT"UM WIEVIEL DOLLAR (- ODER +) WILLST DU"
15207 PRINT"DAS GELDVOLUMEN ERHOEHEN BZW. VERRIN-"
15208 INPUT"GERN";X$:X=VAL(X$):IFX=0THENRETURN
15210 IFKK(SP)+X<0ORKK(SP)+X>5000GOTO15205
15215 IFKA(SP)<XGOTO1125
15220 KA(SP)=KA(SP)-X:KK(SP)=KK(SP)+X:RETURN
15300 IFKG(SP)<>LNTHENPRINT"DIESER LADEN GEHOERT DIR NICHT!":GOTO1100
15305 IFINT(RND(1)*3)<>0ANDKK(SP)<>0GOTO15310
15306 PRINT"ALLE SCHULDNER HABEN PUENKTLICH GE-":PRINT"ZAHLT!":GOTO1100
15310 PRINT"EINER DEINER KLIENTEN HAT NOCH GELD. ER"
15311 PRINT"LAUERT DIR MIT EINEM GEWEHR AUF, ALS DU KASSIEREN WILLST!"
15312 GOSUB1100:BN$(0)="SCHULDNER":GZ(0)=1:E=35:W=6:GOSUB5000
15315 IFS=2THENRETURN
15320 P=INT(RND(1)*1000)+500:POKERA,11:POKERA+1,11
15321 PRINT"ER HATTE NOCH"P"$ DABEI!":KA(SP)=KA(SP)+P:X=2:GOSUB1160:GOTO1100

16000 REM
16010 PRINT"WAEHLE, MEIN FREUND:":PRINT"1 - POKER":PRINT"2 - BLACK JACK"
16011 PRINT"3 - ROULETTE"
16015 INPUT"WAS SOLL'S SEIN:";X:IFX=0THENRETURN
16016 IFX<1ORX>3THENPRINT"";:GOTO16015
16020 PRINT"DU HAST"KA(SP)"$. DEIN EINSATZ:":INPUTP:IFP=0ORP<0 THENRETURN
16025 IFP>KA(SP)GOTO1125
16026 KA(SP)=KA(SP)-P:PRINT"DU BEGIBST DICH AN DEN SPIELTISCH..."
16030 FORT=1TO1000:NEXT:IFINT(RND(1)*(1+X))=0THENP=INT(P*(.5+X)):GOTO16040
16035 PRINT"LEIDER VERLOREN!":GOTO1100
16040 PRINT"DU HAST"P"$ GEWONNEN!":KA(SP)=KA(SP)+P:GOTO1100

17000 REM
17005 IFRA(SP)>1GOTO17007
17006 PRINT"'VERSCHWINDE, DU MILCHGESICHT!'":GOTO1100
17007 IFLL(SP)<>20*LA+LNGOTO17010
17008 PRINT"VOR DEM LADEN ERWARTET DICH DIE"
17009 PRINT"POLIYEI!":GOSUB1100:KF$="KS":GOTO26000
17010 ONWGOTO17015,17100,17200,17300
17015 PRINT"":A=SP:B=1:GOSUB1350
17016 IFBT<30THENPRINT"DU SCHINDEST KEINEN EINDRUCK...":PRINT:GOTO17020
17017 IFLN=1ORLN=7ORLN=9GOTO17500
17020 PRINT"DER KERL RUFT DIE POLIZEI!!!":GOSUB1100:KF$="KS":GOTO26000
17100 IFLN=1ORLN=4ORLN=5GOTO17500
17105 PRINT"'SORGE DOCH SELBST FUER DEINE ALTE!'":GOTO1100
17200 IFLN=2ORLN=3ORLN=8GOTO17500
17205 PRINT"'DAS WIRD ABER NARBEN-JACK INTERES-"
17206 PRINT"SIEREN!'":GOSUB1100
17210 BN$(0)="JACK'S GANG":GZ(0)=3-2*(GZ(SP)>5):W=7:E=30:KF$="KSGL":GOSUB5000
17215 IFS=2THENRETURN
17220 POKERA,4:POKERA+1,4:PRINT"JACK VERZIEHT SICH ERSTMAL AUS DIESEM"
17221 PRINT"GEBIET...":GOSUB1100
17225 GOTO17500
17300 A=SP:B=1:GOSUB1350:IF(LN=1ORLN=3ORLN=6)ANDIN>=30GOTO17500
17305 PRINT"'ACH, DIE ALTE BULLEN-MASCHE! DAMIT"
17306 PRINT"NEPPT IHR KEINEN MEHR!'":GOTO1100
17500 X=2:GOSUB1160:IFINT(RND(1)*3)=0GOTO17530
17505 P=INT(RND(1)*200)+800-300*(LN=2)-200*(LN=7)-200*(LN=9)+600*(W=2)
17510 ONWGOTO17511,17515,17520,17525
17511 PRINT"'I..I..ICH Z..ZAHLE JA SCHON! HI..HIER"
17512 PRINT"SIND"P"$!'":GOTO17550
17515 PRINT"'DEINE ARME MA (SCHNIEF)! GIB IHR DIE":PRINT""P"$ HIER!'"
17516 LL(SP)=0:GOTO17550
17520 PRINT"'ICH HABE"P"DOLLAR, REICHT DAS?'":GOTO17550
17525 PRINT"'VERDAMMT! WER HAT MIR NUR BLUETEN"
17526 PRINT"FUER"P"$ ANGEDREHT...?'":GOTO17550
17530 P=INT(RND(1)*100)+100:PRINT"'ICH HABE LEIDER NUR"P"$!"
17550 KA(SP)=KA(SP)+P:PRINT"WAS MACHST DU:":PRINT"1 ANGEBOTENES GELD NEHMEN"
17551 PRINT"2 LADEN DEMOLIEREN":PRINT"3 BESITZER FERTIGMACHEN"
17555 GETX$:IFX$<"1"ORX$>"3"GOTO17555
17560 ONVAL(X$)GOTO17565,17570,17580
17565 RETURN
17570 IFLN<>2ANDLN<>6ANDLN<>7ANDLN<>8GOTO17575
17571 PRINT"'SAUBANDE! DAS WERDET IHR BUESSEN!'"
17572 PRINT"(DER KERL WINKT EIN PAAR SCHLAEGER":PRINT"HERAN!)":GOSUB1100
17573 BN$(0)="SCHLAEGER":GZ(0)=5:W=3:E=20:KF$="KSGL":GOSUB5000:IFS=2THENRETURN
17575 POKERA,4:POKERA+1,4:P=INT(RND(1)*100)+300
17576 PRINT"DU HAST KLEINHOLZ AUS DEM LADEN"
17577 PRINT"GEMACHT. IN DER KASSE WAREN"P"$!"
17578 KA(SP)=KA(SP)+P:X=1:GOSUB1160:GOTO1100
17580 IFLN<>1ANDLN<>4GOTO17590
17585 PRINT"ER VERSPRICHT DIR EINEN SCHOENEN GRAB-"
17586 PRINT"STEIN UND LAEDT SEINE "WA$(7)"!":GOSUB1100
17587 BN$(0)="LADENBESITZER":GZ(0)=1:W=7:E=30:KF$="KSGL":GOSUB5000
17588 IFS=2THENRETURN
17590 P=INT(RND(1)*100)+200:POKERA,4:POKERA+1,4
17591 PRINT"DER AUFMUEPFIGE KERL IST HIN."P"$"
17592 PRINT"HATTE ER DER TASCHE!":X=1:GOSUB1160:GOTO17578

18000 REM
18010 ONWGOTO18035,18015
18015 PRINT"EIN U-BAHN-TICKET KOSTET DICH"
18020 PRINT"50 $. ";:GOSUB1110:IFX$="N"THENRETURN
18025 IFKA(SP)<50GOTO1125
18030 KA(SP)=KA(SP)-50
18035 PRINT"WELCHEN SPIELER SETZT DU ALS DIEB EIN:":GOSUB1130:IFY=0THENRETURN
18039 X=1:GOSUB1160:PRINT"DU STIEHLST...":FORT=1TO500:NEXT
18040 IFINT(RND(1)*15)=10GOTO18052
18041 IFINT(RND(1)*(IN/10))GOTO18045
18042 PRINT"...NICHTS! DENN DU WIRST ERWISCHT!":GOSUB1100:GOTO26020
18045 ONINT(RND(1)*4)-(W=2)-(LA<>9)GOTO18047,18048,18049,18050,18051
18046 PRINT"...EINE HANDTASCHE - NICHTS WERTVOLLES!":GOTO1100
18047 PRINT"...'NEN FOTOAPPARAT! ER BRINGT 50 $.":KA(SP)=KA(SP)+50:GOTO1100
18048 PRINT"...EINE FALSCHE PERLENKETTE!":GOTO1100
18049 PRINT"...EINE ARMBANDUHR FUER 100 $!":KA(SP)=KA(SP)+100:GOTO1100
18050 PRINT"...'NE BRIEFTASCHE MIT 500 $!":KA(SP)=KA(SP)+500:GOTO1100
18051 PRINT"...EINEN DIAMANTEN! DER IST 800 $ WERT!":KA(SP)=KA(SP)+800:GOTO1100
18052 PRINT"...EINE ANLEITUNG -DER SAFEKNACKER- ??!":S9(SP)=5:GOTO1100

19000 REM
19005 ONWGOTO19010,19050,19015
19010 LN=5:LA=2:GOTO3000
19015 IFTP(SP)<>1THENPRINT"KEIN POSTZUG ZU SEHEN...":GOTO1100
19016 IFGZ(SP)<3THENPRINT"DU HAST ZU WENIG GANGSTER!":TP(SP)=0:GOTO1100
19020 SYSLH,"PZUG-PIC"
19025 POKERA,11:POKERA+1,8:PRINT"DU STUERMST IN DEN PANZERWAGGON UND"
19026 PRINT"BEGINNST GERADE EINZUSACKEN, ALS DU AUF"
19027 PRINT"DREI NETTE HERREN AUFMERKSAM WIRST...":GOSUB1100
19030 BN$(0)="WACHEN":GZ(0)=3:W=7:E=30:KF$="KPZUG":GOSUB5000:IFS=2GOTO26020
19040 GOTO20050
19050 W=1:GOTO18035

20000 REM
20003 IFRA(SP)<3THENPRINT"WERDE ERST '"RA$(3)"'!":GOTO1100
20004 IFLL(SP)=20*LA+LNGOTO17008
20005 ONWGOTO20009,20100
20009 IFGZ(SP)=1THENPRINT"DU BRAUCHST EINEN BEGLEITER!":GOTO1100
20010 IFINT(RND(1)*3)=0GOTO20050
20011 PRINT"LEIDER HAST DU DIE DREI WACHMAENNER AM"
20012 PRINT"EINGANG UEBERSEHEN...":GOSUB1100:GZ(0)=3-(LN=1)
20015 BN$(0)="WACHMAENNER":E=30:W=6:KF$="KB":GOSUB5000:IFS=2GOTO26020
20050 P=INT(RND(1)*3000)+4000-500*(LA=10ANDLN=1):X=TP(SP)
20051 IF(X=1ANDLA=9)OR(X=2ANDLA=10ANDLN=2)OR(X=3ANDLA=13)THENTP(SP)=0:P=P+3000
20055 POKERA,8:POKERA+1,8:PRINT"DU HAST ES GESCHAFFT!DEINE BEUTE"
20060 PRINT"BETRAEGT"P"$!":KA(SP)=KA(SP)+P:X=4:GOSUB1160:GOTO1100
20100 A=SP:B=1:GOSUB1350:IFIN>=40ANDKR>=15ANDBT>=20GOTO20102
20101 PRINT"DU MUSST NOCH TRAINIEREN!":GOTO1100
20102 POKERA,0:POKERA+1,0
20104 PRINT"WER SOLL DEN KASTEN KNACKEN:":GOSUB1130:IFY=0THENRETURN
20105 PRINT"'HHM. MAL SEHEN. ICH VERSUCH'S MIT DEM"
20106 PRINT"STETHOSKOP...' (DREHEN DER CODERAEDER"
20107 PRINT"MIT F1, F3 UND F5!)":GOSUB1100
20110 SYSBL,"TRS1":POKESI+24,15:FORI=0TO2:RD(I)=1+I:CD(I)=INT(RND(1)*10):NEXT
20111 Y=20+INT(IN/10)+3*(LN=1)+S9(SP):S9(SP)=S9(SP)-1:IFS9(SP)<0THENS9(SP)=0
20115 GETX$:X=ASC(X$+CHR$(0)):IFX<133ORX>135GOTO20115
20116 X=X-133:RD(X)=RD(X)+1:IFRD(X)=10THENRD(X)=0
20120 POKE211,16+3*X:POKE214,7:SYSCS:PRINT""CHR$(48+RD(X))
20125 IFINT(RND(1)*(IN/8))=0ORRD(X)<>CD(X)THENSYSSO,7:GOTO20135
20130 SYSSO,8:FORI=0TO2:IFRD(I)=CD(I)THENNEXT:GOSUB1190:GOTO20150
20135 Y=Y-1:IFY>0GOTO20115
20140 GOSUB1190:SYSSO,6:POKERA,0:POKERA+1,0
20141 PRINT"'TEUFEL...! DA IST WAS SCHIEFGEGANGEN!"
20142 PRINT"ES KOMMT JEMAND!'":GOSUB1100:KF$="KB":GOTO26000
20150 SYSBL,"TRS2":POKE198,0:WAIT198,1:POKE198,0:X=-1:GOSUB1160:GOTO20050

21000 REM
21005 ONWGOTO26045,21010,21100
21010 PRINT"AHA! FUER EINEN MONAT UNTAETIGKEIT"
21011 INPUT"NEHME ICH 1000 $. WIEVIELE MONATE:";X:P=1000*X:IFX=0THENRETURN
21015 IFKA(SP)<PTHENPRINT"SIE SIND LEIDER NICH FLUESSIG!":GOTO1100
21020 KA(SP)=KA(SP)-P:PL(SP)=PL(SP)+X+1:X=2:GOSUB1160
21025 PRINT"DANKE SEHR! NEHMEN SIE DEN HINTER-"
21030 PRINT"AUSGANG!":PO(SP)=911:GOTO1100
21100 X=1:FORI=1TOSZ:IFGS(I)THENG(X)=I:X=X+1
21105 NEXT:IFINT(RND(1)*3)=0ANDRA(SP)>4THENG(X)=5:X=X+1:SP$(5)="IRGENDJEMANDEN"
21110 IFX=1THENPRINT"ES IST NIEMAND INHAFTIERT.":GOTO1100
21115 PRINT"WEN WILLST DU BEFREIEN:"
21116 X=X-1:FORI=1TOX:PRINT""MID$(STR$(I),2)" "SP$(G(I)):NEXT
21120 GETX$:IFX$=""ORVAL(X$)>XGOTO21120
21125 G=VAL(X$):IFG=0THENRETURN
21130 P=500*INT(RND(1)*5)+3000:PRINT"DU BRAUCHST"P"$, UM DIE WAERTER ZU"
21131 PRINT"BESTECHEN. ";:GOSUB1110:IFX$="N"THENRETURN
21135 IFKA(SP)<PGOTO1125
21140 KA(SP)=KA(SP)-P
21200 PRINT"DU KONNTEST DEN GEFANGENEN BEFREIEN!":GOSUB1100:IFG(G)<>5GOTO21250
21205 IFGZ(SP)=10THENPRINT"ER BEDANKT SICH UND VERSCHWINDET...":GOTO1100
21210 X=GZ(SP)+1:GZ(SP)=X:GN$(SP,X)="KNASTI":GW(SP,X)=0:GE$(SP,X)="05100540"
21215 RETURN
21250 X=G(G):PRINT" "SP$(X)"! ":PRINT"DU BIST VON "SP$(SP)" AUS DEM"
21251 PRINT"KNAST BEFREIT WORDEN. WIEVIEL ZAHLST DU"
21252 PRINT"IHM ZUM DANK (0 -"KA(X)"):";:INPUTX$:Y=VAL(X$):IFY>KA(X)GOTO21250
21253 IFY>KA(X)ORY<0THENPRINT"";:GOTO21252
21255 KA(X)=KA(X)-Y:KA(SP)=KA(SP)+Y:GS(X)=0:X=2:GOSUB1160:RETURN

22000 REM
22005 ONWGOTO22010,22100
22010 X=GZ(SP):P=1000*X:PRINT"GUT, MEIN SOHN. ";
22011 IFX=1THENPRINT"FUER EINEN PASS":GOTO22013
22012 PRINT"FUER"X"PAESSE"
22013 PRINT"MACHT DAS"P"$. ";:GOSUB1110:IFX$="N"THENRETURN
22014 IFKA(SP)<PGOTO1125
22015 PRINT"HIER, NOCH DRUCKFRISCH, HE, HE!"
22020 KA(SP)=KA(SP)-P:AG(SP)=AG(SP)OR1:X=1:GOSUB1160:GOTO1100
22100 PRINT"NNG...HOECHST UNGERN! WIEVIEL DOLLAR"
22105 INPUT"WILLST DU ANLEGEN (0-5000)";Q:IFQ<=0THENRETURN
22106 IFQ>5000ORQ>KA(SP)THENPRINT"";:GOTO22105
22110 P=INT(RND(1)*Q/2)+Q+100:PRINT"ICH GEBE DIR"P"$ BLUETEN DAFUER.":PRINT
22115 GOSUB1110:IFX$="N"THENRETURN
22120 KA(SP)=KA(SP)-Q+P:AG(SP)=AG(SP)OR2:X=1:GOSUB1160:RETURN

23000 REM
23001 SYSLH,"GTP-PIC"
23005 POKERA,0:POKERA+1,6:PRINT" GELDTRANSPORT-UEBERFALL "
23010 IFGZ(SP)<3THENPRINT"DU HAST ZUWENIG GANGSTER!":TP(SP)=0:GOTO1100
23015 PRINT"LEIDER WIRD DER GELDTRANSPORT VON EINER"
23020 PRINT"POLIZEIESKORTE BEGLEITET!":GOSUB1100
23025 BN$(0)="ESKORTE":GZ(0)=10:E=50:W=7:KF$="KGTP":GOSUB5000:IFS=2GOTO26020
23030 X=4:GOSUB1160:X5%(SP)=1:GOTO20050

24000 REM
24005 BN$(0)="LEIBWAECHTER":GZ(0)=5:E=20:W=7:KF$="KS":GOSUB5000:IFS=2GOTO26020
24010 BN$(0)="BUERGERMEISTER":GZ(0)=1:E=30:W=1:KF$="KS":GOSUB5000:IFS=2GOTO26020
24015 POKERA,0:POKERA+1,6:PRINT" MORD!!! "
24016 PRINT"FUER DEINE RUCHLOSE TAT BEKOMMST DU AUCH"
24017 PRINT"NOCH 7000 $ UND EINEN NEUEN PASS!"
24020 KA(SP)=KA(SP)+7000:AG(SP)=AG(SP)OR1:TP(SP)=0:X6%(SP)=1:GOTO1100

25000 POKERA,9:POKERA+1,9:PRINT" "SP$(SP)":JOB ALS ":PRINT
25010 ONJO(SP)GOTO25015,25100,25015,25200
25015 PRINT" RAUSSCHMEISSER "
25020 PRINT"DU WARTEST AUF EIN ZEICHEN DEINES CHEFS,"
25021 PRINT"EINEN UNGEBETENEN GAST ZU ENTFERNEN...":FORT=1TO2000:NEXT
25025 IFINT(RND(1)*2)=0THENPRINT"HEUTE GAB ES NICHTS!":GOSUB1100:GOTO25550
25030 PRINT"EIN BURSCHE FAENGT AN, ZU RANDALIEREN!":PRINT"TU ETWAS!":GOSUB1100
25035 GZ(0)=1:KF$="KP":ONINT(RND(1)*3)GOTO25041,25042
25040 BN$(0)="WURSTFINGER-FRED":W=0:E=30:GOTO25045
25041 BN$(0)="AFFENFACE-ALF":W=1:E=20:GOTO25045
25042 BN$(0)="DER SCHLACHTER":W=3:E=20
25045 GOSUB5000:GOTO25500
25100 PRINT" CROUPIER "
25105 PRINT"DU MUSST ALS"
25106 PRINT"CROUPIER NATUERLICH FALSCHSPIELEN."
25107 PRINT"WELCHE TRICKS WILLST DU ANWENDEN:"
25110 PRINT"1 TRICKSPIEGEL":PRINT"2 GEZINKTE KARTEN"
25111 PRINT"3 BREMSE AM ROULETTE"
25115 GETX$:IFX$<"1"ORX$>"3"GOTO25115
25120 X=VAL(X$):IFINT(RND(1)*(6-X))=0GOTO25130
25125 P=INT(RND(1)*100*X)+300:PRINT"DU MACHST GEWINNE UND ERHAELTST"
25126 PRINT"EINEN BONUS VON"P"$.":KA(SP)=KA(SP)+P:GOSUB1100:GOTO25550
25130 PRINT"EIN KUNDE HAT DEINE TRICKS DURCHSCHAUT"
25131 PRINT"UND ZIEHT EIN MESSER!":GOSUB1100
25135 BN$(0)="SPIELER":GZ(0)=1:E=10:W=1:KF$="KP":GOSUB5000
25140 GOTO25500
25200 PRINT" KILLER "
25205 PRINT"DU HAST DEN TYP AUF DEM FOTO GEFUNDEN"
25206 PRINT"UND LOCKST IHN IN EINE SEITENSTRASSE...":GOSUB1100
25210 BN$(0)="OPFER":GZ(0)=1:W=0:E=20:KF$="KM":GOSUB5000:GOTO25500
25500 IFS=1GOTO25550
25505 POKERA,9:POKERA+1,9:PRINT"DU HAST DEINEN AUFTRAG NICHT"
25506 PRINT"ERLEDIGT!"
25510 X=-2:GOSUB1160:JO(SP)=0:GOTO1100
25550 JD(SP)=JD(SP)-1:IFJD(SP)<>0THENRETURN
25555 POKERA,9:POKERA+1,9:PRINT"DU HAST DEINEN JOB ERLEDIGT UND ER-"
25556 PRINT"HAELTST DEINEN LOHN. ES SIND"JL(SP)"$!":KA(SP)=KA(SP)+JL(SP)
25560 X=3+3*(JO(SP)=2):GOSUB1160:JO(SP)=0:GOTO1100

26000 BN$(0)="DIE POLIZEI":GZ(0)=5+INT(RND(1)*RA(SP)/2)
26010 W=5-2*(RA(SP)>5):E=20+2*(RA(SP)-1)-INT(RND(1)*21):GOSUB5000
26015 IFS=1THENX=2:GOSUB1160:RETURN
26020 POKERA,6:POKERA+1,6:PRINT"DU BIST VON DER POLIZEI GEFASST WORDEN."
26021 IFPL(SP)ANDINT(RND(1)*2)<>0GOTO26037
26022 PRINT"WAS NUN:":PRINT"1 POLIZISTEN BESTECHEN":PRINT"2 FLUCHTVERSUCH"
26023 PRINT"3 ERGEBEN"
26025 GETX$:IFX$<"1"ORX$>"3"GOTO26025
26030 ONVAL(X$)GOTO26035,26040,26045
26035 P=500+500*RA(SP):PRINT"SIE VERLANGEN"P"$. ";
26036 GOSUB1110:IFX$="N"THENX=2:GOSUB1160:GOTO26045
26037 IFKA(SP)<PTHENGOSUB1125:GOTO26045
26038 KA(SP)=KA(SP)-P:IFINT(RND(1)*5)=0GOTO26045
26039 PRINT"DIE BULLEN LASSEN DICH GEHEN!":GOTO1100
26040 IFINT(RND(1)*TR(SP)/11)=0THENX=-5:GOSUB1160:GOTO26042
26041 PRINT"DU BIST GERADE NOCH EINMAL ENTKOMMEN!":X=2:GOSUB1160:GOTO1100
26042 PRINT"DIE POLIZISTEN SCHNAPPEN DICH"
26043 PRINT"DU MUSST DICH ERGEBEN!":GOSUB1100
26045 TP(SP)=0:X=2:GOSUB1160:GS(SP)=INT(RA(SP)/2+.5)
26050 PRINT"DEINE GERICHTSVERHANDLUNG...":IFRA(SP)<5GOTO26075
26055 PRINT"WILLST DU EINEN ANWALT (J/N) ?":GOSUB1115:IFX$="N"GOTO26075
26060 INPUT"WIEVIEL LEGST DU AN (0-10000$)";X$:X=VAL(X$):IFX=0GOTO26075
26061 IFX>KA(SP)ORX<0THENPRINT"";:GOTO26060
26062 KA(SP)=KA(SP)-X:Y=INT(RND(1)*(X/1000+1))+1
26065 GS(SP)=GS(SP)-Y:IFGS(SP)<0THENGS(SP)=0
26070 IFGS(SP)=0THENPRINT"DU WIRST FREIGESPROCHEN!":MS=0:GOTO1100
26071 PRINT"DEIN ANWALT BOXT EINE STRAFE VON"
26072 PRINT"NUR"GS(SP)"MONAT(EN) HERAUS.":GOTO26080
26075 PRINT"DU WANDERST FUER"GS(SP)"MONAT(E) HINTER":PRINT"GITTER!"
26080 MS=0:JO(SP)=0:X=-10:GOSUB1160:PO(SP)=911:GOTO1100

###############
# BANDENKRIEG #
###############
27000 IFSZ=1THENPRINT"BEI SOLO-SPIEL NICHT MOEGLICH!":GOTO1100
27001 IFJA<1925+4/12THENPRINT"ERST AB 4/1925 MOEGLICH!":GOTO1100
27005 PRINT""TAB(13)"BANDENKRIEG!"
27006 FORI=1TO20:POKERA,0:FORT=1TO50:NEXT:POKERA,2:NEXT
27007 PRINT" MIT WEM WILLST DU DICH ANLEGEN:"
27010 FORI=1TOSZ:IFI=SPGOTO27013
27011 PRINT" "MID$(STR$(I),2)" "BN$(I)" "KA(I)"$  ";
27012 PRINTLEFT$("",1+GZ(I))
27013 NEXTI
27015 GETX$:IFX$=""GOTO27015
27016 US=VAL(X$):IFUS=0THENRETURN
27017 IFUS<1ORUS>SZORUS=SPGOTO27015
27018 IFGS(US)GOTO27100
27020 KS(1)=US:KS(2)=SP:KF$="KS":GOSUB30000:A=KS(S):B=KS(1-(S=1))
27025 P=INT(RND(1)*KA(B)/6)+INT(KA(B)/4)
27026 POKERA,2:PRINT""SP$(A)", DU FINDEST BEIM PLUENDERN"
27027 PRINT"DEINES GEGNERS"P"$. AUSSERDEM ER-"
27028 PRINT"BEUTEST DU DEN ALKOHOLVORRAT.":IFTM(B)=0GOTO27035
27030 PRINT"WILLST DU AUCH NOCH DEN "TM$(TM(B))
27031 PRINT"MITNEHMEN (J/N) ?":GOSUB1115:IFX$="J"THENTM(A)=TM(B):TM(B)=0
27035 KA(A)=KA(A)+P:KA(B)=KA(B)-P:AG(A)=AG(A)OR(AG(B)AND1):AG(B)=AG(B)AND254
27040 X=TK(TM(A))-TA(A):IFX>TA(B)THENX=TA(B)
27041 TA(A)=TA(A)+X:TA(B)=TA(B)-X:X=3:GOSUB1160:Y=SP:SP=B:X=-1:GOSUB1160:SP=Y
27045 MS=MS-10:GOTO1100
27100 PRINT""SP$(US)" SITZT IM KNAST. EIN MIT-"
27105 PRINT"GEFANGENER WILL IHN FUER DICH 'AUF-"
27110 PRINT"MISCHEN'. ER VERLANGT 3000 $. ";:GOSUB1110:IFX$="N"THENRETURN
27115 IFKA(SP)<3000GOTO1125
27120 KA(SP)=KA(SP)-3000:PRINT"VERTEIDIGE DICH, "SP$(US)"!":GOSUB1100
27125 BN$(0)="MR.BONEBREAKER":GZ(0)=1:GW(0,1)=3:EC(1)=50:KS(2)=0
27130 Z1=GW(US,1):Z2=GZ(US):GW(US,1)=0:GZ(US)=1:KS(1)=US:KF$="KG":GOSUB30000
27135 GW(US,1)=Z1:GZ(US)=Z2:IFS=2GOTO27146
27140 X=INT(RND(1)*2)+1:PRINT""SP$(US)"! DEINE STRAFE WIRD WEGEN"
27141 PRINT"DIESER SCHLAEGEREI UM"X"MONAT(E)":PRINT"VERLAENGERT!"
27145 GS(US)=GS(US)+X:GOTO27150
27146 A=SP:B=1:GOSUB1350:EN=0:GOSUB1365
27150 MS=MS-10:X=2:GOSUB1160:GOTO1100

#########
# KAMPF #
#########
30000 SYSBL,KF$:FORI=1TO2:FORJ=1TOGZ(KS(I)):KP(I,J)=129-18*(I=2)+P(J)
30010 POKEBR+KP(I,J),193:POKEFR+KP(I,J),2-4*(I=2):NEXTJ
30015 POKE211,-20*(I=2):POKE214,16:SYSCS:PRINTBN$(KS(I)):NEXT
30020 IFKS(2)=0THENFORI=1TOGZ(0):RI(I)=-1:NEXT
30025 POKE211,0:POKE214,14:SYSCS:PRINT"DER KAMPF BEGINNT..."
30030 FORT=1TO2000:NEXT:GOSUB1450
30100 S=1:F=0:V(1)=0:V(2)=0
30105 F=F+1
30106 FORI=1TOGZ(KS(1-(S=1))):IFKP(1-(S=1),I)<0THENNEXTI:GOTO30500
30107 IFF<>GZ(KS(S))+1GOTO30109
30108 S=1-(S=1):F=1:X=-20*(S=1):POKE214,20:SYSCS:GOSUB1400
30109 IFKP(S,F)<0GOTO30105
30110 IFKS(S)=0THENGOSUB30400:GOTO30105
30115 X=-20*(S=2):POKE211,X:POKE214,18:SYSCS:PRINT"SPIELER   "F
30116 PRINT:GOSUB1400:A=KS(S):B=F:POKE199,1:GOSUB1300
30120 SYSI1,BR+KP(S,F),FR+KP(S,F),193,2-4*(S=2),32,11

30125 GETX$:IFX$=""GOTO30125
30130 X=0:IFX$=":"THENX=-1:GOTO30140
30131 IFX$=";"THENX=1:GOTO30140
30132 IFX$="@"THENX=-40:GOTO30140
30133 IFX$="/"THENX=40:GOTO30140
30134 IFX$=CHR$(13)THENSYSIE:GOSUB30200:GOTO30105
30135 IFX$=" "THENGOTO30155
30136 IFX$="Q"THENSYSIE:S=1-(S=1):GOTO30500
30139 GOTO30125
30140 P=BR+KP(S,F)+X
30145 IF(PEEK(P)<>32ANDPEEK(P)<>96)ORP<BRORP>BR+520GOTO30125

30150 KP(S,F)=KP(S,F)+X
30155 SYSIE:POKEBR+KP(S,F),193:POKEFR+KP(S,F),2-4*(S=2):GOTO30105
30200 POKEBR+KP(S,F),193:POKEFR+KP(S,F),0
30205 GETX$:IFX$=""GOTO30205
30206 IFX$="@"THENX=-40:GOTO30211
30207 IFX$="/"THENX=40:GOTO30211
30208 IFX$=":"THENX=-1:GOTO30211
30209 IFX$=";"THENX=1:GOTO30211
30210 GOTO30205
30211 POKEFR+KP(S,F),2-4*(S=2)
30215 R=2:W=GW(KS(S),F):IFW>3THENR=15
30216 IFW=6ORW=7THENR=20
30220 P=KP(S,F):GOTO30225
30224 POKEBR+P,AZ:POKEFR+P,AF
30225 P=P+X:R=R-1:IFP<0ORP>520ORPEEK(BR+P)=160ORPEEK(BR+P)=156ORR=0GOTO30235
30226 IFPEEK(BR+P)=193AND(PEEK(FR+P)AND15)=2-4*(S=1)GOTO30240
30230 AZ=PEEK(BR+P):AF=PEEK(FR+P):POKEBR+P,42:POKEFR+P,0:GOTO30224
30235 POKE214,14:SYSCS:PRINT"VERFEHLT!":FORT=1TO1000:NEXT:GOTO1450
30240 FORI=1TO1-2*(W=4)
30241 POKEBR+P,170:POKEFR+P,0:SYSSO,WS(W):POKEBR+P,193:POKEFR+P,2-4*(S=1)
30243 NEXTI
30245 IFKS(S)=0THENBT=30:KR=30:GOTO30247
30246 A=KS(S):B=F:GOSUB1350
30247 IFINT(RND(1)*TS(W))=0ORINT(RND(1)*(KR/10+1))=0GOTO30235
30250 X=1-(S=1):FORI=1TOGZ(KS(X)):IFKP(X,I)<>PTHENNEXT
30255 Y=INT(RND(1)*TG(W)+BT/10)+1:IFKS(X)=0GOTO30275
30260 A=KS(X):B=I:GOSUB1350:EN=EN-Y:IFEN<0THENEN=0
30265 GOSUB1365:IFEN=0GOTO30300
30266 GOTO30350

30275 EC(I)=EC(I)-Y:IFEC(I)<0THENEC(I)=0
30280 IFEC(I)=0GOTO30300
30281 GOTO30350

30300 POKE214,14:SYSCS:IFKS(X)=0THENPRINT"DER GEGNER IST TOT!":GOTO30310
30305 PRINT"TREFFER! SPIELER"I"IST AM ENDE!"
30310 FORT=1TO2000:NEXT:GOSUB1450:KP(X,I)=-1:POKEBR+P,32:V(X)=V(X)+1:RETURN

30350 POKE214,14:SYSCS:PRINT"TREFFER! ENERGIE -"MID$(STR$(Y),2)"!"
30355 FORT=1TO2000:NEXT:GOSUB1450:RETURN

30400 POKE211,20:POKE214,18:SYSCS:PRINT"SPIELER"F
30405 Y=INT(KP(S,F)/40):SYSCR,KP(S,F)-40*Y,Y:X=PEEK(UA)-1:Y=PEEK(UA+1)-40
30410 IFPEEK(UA+2)=1ORPEEK(UA+3)=1GOTO30420
30415 IFINT(RND(1)*2)=0ORGW(KS(S),F)<4GOTO30450
30420 IFX=0THENX=Y:GOTO30215
30421 IFY<>0GOTO30450
30425 GOTO30215

30450 IFX<>0ANDRI(F)<>(1+2*(X=1))THENP=X:GOSUB30490:IFP=0THENRETURN
30451 IFY<>0ANDRI(F)<>(40+80*(Y=1))THENP=Y:GOSUB30490:IFP=0THENRETURN
30455 IFX<>0ANDRI(F)<>(1+2*(X=1))GOTO30460
30456 IFRI(F)<>-1THENP=1:GOSUB30490:IFP=0THENRETURN
30457 IFRI(F)<>1THENP=-1:GOSUB30490:IFP=0THENRETURN
30458 GOTO30465
30460 IFY<>0ANDRI(F)<>(40+80*(Y=1))GOTO30465
30461 IFRI(F)<>-40THENP=40:GOSUB30490:IFP=0THENRETURN
30462 IFRI(F)<>40THENP=-40:GOSUB30490:IFP=0THENRETURN
30465 RETURN
30490 Q=KP(S,F)+P:IFQ<0ORQ>520OR(PEEK(BR+Q)<>32ANDPEEK(BR+Q)<>96)THENRETURN
30491 POKEBR+KP(S,F),32:KP(S,F)=Q:POKEBR+KP(S,F),193:POKEFR+KP(S,F),6
30492 RI(F)=P:P=0:RETURN
30500 POKERA,0:POKERA+1,0:PRINT" SIEGER: "BN$(KS(S))"!!! "
30510 PRINT"VERLUSTE DER SPIELER:"
30515 PRINT""BN$(KS(1))":"V(1):PRINT""BN$(KS(2))":"V(2)
30520 PRINT:GOTO1100
31000 TP(SP)=0:POKERA,8:POKERA+1,8:IFINT(RND(1)*5)=0GOTO31050
31005 P=INT(RND(1)*9500)+5500:PRINT"DEIN KAPITAL HAT IM WAFFENHANDEL GE-"
31010 PRINT"ARBEITET. DU ERHAELTS"P"$ ZURUECK!":KA(SP)=KA(SP)+P:GOTO1100
31050 PRINT"DIE WAFFENHAENDLER, DENEN DU 5000 $ AN-"
31051 PRINT"VERTRAUT HAST, WURDEN GESCHNAPPT!":GOTO1100

40000 REM ENDE
40100 GOSUB4500:POKERA,1:POKERA+1,1:G=1:G(G)=1
40105 FORI=2TOSZ:IFGF(I)>GF(G(G))THENG=1:G(G)=I:GOTO40110
40106 IFGF(I)=GF(G(G))THENG=G+1:G(G)=I
40110 NEXTI:IFG>1GOTO40150
40115 PRINT""SP$(G(G))" HAT GEWONNEN!"
40116 PRINT"DU WARST VON ALLEN DER BRUTALSTE,"
40117 PRINT"GEMEINSTE UND SCHLAUESTE!":GOTO40165
40150 PRINT"DIESMAL HABEN MEHRERE DIE GLEICHEN":PRINT"PUNKTE:"
40155 FORI=1TOG:PRINT""SP$(G(I)):NEXT
40160 PRINT"ALLE WAREN GLEICH FIESE GANGSTER!"
40165 POKE198,0:WAIT198,1
40166 POKE198,0:WAIT198,1:RUN
50005 DATA SLW,PUB,WAF,AUT,KDH,SPH,SGL,SUB,BHF,BAN,POL,BLE
50100 DATA "HAENDE",0,2,2,1,"MESSER",50,3,5,0,"KNUEPPEL",100,4,3,1
50105 DATA "SCHLAGKETTE",500,4,4,4,"WURFSTERNE",3000,2,7,0
50110 DATA "REVOLVER",4000,5,10,2,"GEWEHR",4500,5,12,2
50115 DATA "MASCHINENPISTOLE",8000,6,15,10,"HANDGRANATEN",10000,7,18,3
50200 DATA "ANFAENGER","SCHLAEGER","KLEINER FISCH","LANGFINGER","GANOVE"
50205 DATA "MAFIOSI","BULLENSCHRECK","MEUCHELMOERDER","GANGSTERBOSS"
50210 DATA "CHEF DER UNTERWELT"
50300 DATA "FUESSE",50,25,"TALBOT 90",100,35,"CHEVY ROADSTER",120,40
50305 DATA "BUICK CENTURY",200,40,"AUBURN MOD.120",150,60,"CITROEN T.A.",100,35
50400 DATA 122,81,161,120,42,202,40,200,1,241
50500 DATA "MIES","GANZ GUT","TODSICHER","LAECHERLICH","MAESSIG","SCHLIMM!"
50505 DATA "BRUTAL","ERSCHRECKEND!"
50600 DATA "PAPIERE","FALSCHGELD"
50700 DATA "ALLE REIN!","NUR NEUE...","PER ABFRAGE"
